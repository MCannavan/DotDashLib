plugins {
    id 'java-library'
    id 'maven-publish'
}
group 'dev.mcannavan'
version = '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.1'

    api 'org.apache.commons:commons-math3:3.6.1'

    implementation 'com.google.guava:guava:33.0.0-jre'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.named('jar') {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version)
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
    withSourcesJar()
    withJavadocJar()
}


publishing {
    repositories {
        maven {
            name = "githubpackages"
            url = "https://maven.pkg.github.com/mcannavan/dotdashlib"
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }

    publications {
        gpr(MavenPublication) {
            groupId 'dev.mcannavan.dotdash'
            artifactId 'dotdash'
            from(components.java)
        }
    }
}
