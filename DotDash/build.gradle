plugins {
    id 'java-library'
    id 'maven-publish'
}
group = 'dev.mcannavan'
version = '1.0.6'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.1'
    testImplementation "org.mockito:mockito-core:3.+"

    api 'org.apache.commons:commons-math3:3.6.1'

    implementation 'com.google.guava:guava:33.0.0-jre'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.named('jar') {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version)
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
    withSourcesJar()
    withJavadocJar()
}


publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/mcannavan/dotdashlib"
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }

    publications {
        gpr(MavenPublication) {
            artifactId 'dotdash'
            from(components.java)
            pom {
                name = "DotDash"
                description = "A small Java library for handling the translation and audio generation of morse code."
                url = "https://github.com/MCannavan/DotDashLib"
                licenses {
                    license {
                        name = "MIT License"
                        url = "https://github.com/MCannavan/DotDashLib/blob/main/LICENSE"
                    }
                }
                developers {
                    developer {
                        id = "MCannavan"
                        name = "Mark Cannavan"
                        email = "Mark@Mcannavan.dev"
                    }
                }
            }
        }
    }
}
