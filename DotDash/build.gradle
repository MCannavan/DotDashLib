plugins {
    id 'java-library'
    id 'maven-publish'
}
group 'dev.mcannavan'
version = '0.12.0'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.1'

    api 'org.apache.commons:commons-math3:3.6.1'

    implementation 'com.google.guava:guava:33.0.0-jre'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.named('jar') {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version)
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
    withSourcesJar()
    withJavadocJar()
}


publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/mcannavan/DotDashLib"
            credentials {
                username = property('publishUsername')  // Use property from gradle.properties
            }
        }
    }

    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}
